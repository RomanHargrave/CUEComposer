apply plugin: 'java'
apply plugin: 'scala'

apply from: 'gradle/javafx.plugin'

sourceCompatibility = 1.7
version = '1.0'

def buildType = 'dev'

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath group: 'eu.appsatori',        name: 'gradle-fatjar-plugin',   version: '0.3'
        classpath group: 'com.github.maiflai',  name: 'gradle-scalatest',       version: '0.6-5-g9065d91'
    }
}

apply plugin: 'com.github.maiflai.scalatest'
apply plugin: 'eu.appsatori.fatjar'

repositories {
    mavenCentral()
}

dependencies {
    compile     group: 'org.scala-lang',            name: 'scala-library',                  version: '2.11.4'
    compile     group: 'org.scala-lang.modules',    name: 'scala-parser-combinators_2.11',  version: '1.0.3'
    compile     group: 'org.scalafx',               name: 'scalafx_2.11',                   version: '2.2.76-R11'
    compile     group: 'org.slf4j',                 name: 'slf4j-api',                      version: '1.7.5'

    compile    (group: 'org.streum',                name: 'configrity-core_2.10',           version: '1.0.0') {
        exclude group: 'org.scalatest'
    }

    compile    (group: 'org.clapper',               name: 'avsl_2.11',                      version: '1.0.2') {
        exclude group: 'javax.mail'
    }

    compile     group: 'org.clapper',               name: 'grizzled-slf4j_2.11',            version: '1.0.2'

    compile     files('lib/javafx-dialogs/javafx-dialogs-0.0.4.jar')

    /*
     * Add SFX2 with menu-dev additions until latest hits maven
     */
    compile     project(':cuelib')

    testCompile group: 'org.scalatest',     name: 'scalatest_2.11', version: '2.2.4'
    testCompile group: 'uk.co.jemos.podam', name: 'podam',          version: '4.7.2.RELEASE'
    testRuntime group: 'org.pegdown',       name: 'pegdown',        version: '1.1.0'
}

processResources {
    exclude 'GPL_HEADER.txt'
    from("${project.projectDir.absolutePath}/LICENSE") {
        into '/'
    }

    if(buildType == 'dist'){
        exclude 'avsl.conf'
        rename 'avsl_dist.conf', 'avsl.conf'
    }
}

fatJarPrepareFiles {
    exclude 'rootdoc.txt'
    exclude 'library.properties'
}

jfxCopyLibs.enabled = false

jar{
    dependsOn configurations.runtime
    from {
        configurations.runtime
                .findAll { !it.name.contains('jfxrt.jar') }
                .collect { it.isDirectory() ? it : zipTree(it) }
                .collect { it.matching {
                    exclude 'LICENSE', 'README'
                } }
    }
}

javafx {
    appID       'CUEComposer'
    appName     'CUE Composer'
    mainClass   'info.hargrave.composer.ComposerLauncher'

    category    = 'Audio'
    copyright   = 'Copyleft Â© 2014 Roman Hargrave'
    description = 'Tool to manipulate CUE files'
    licenseType = 'GPLv3.0'
    menu        = true
    shortcut    = true
}